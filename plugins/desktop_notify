name        'desktop_notify';
description 'Desktop Notifications';
author      'Dagfinn Ilmari MannsÃ¥ker';

api_version 0.1;

my $notify;

hook init => sub {
    my $args = shift;
    $notify = eval { require Desktop::Notify; Desktop::Notify->new }
        or $args->{app}->diag("! Couldn't find Desktop::Notify. desktop_notify plugin is disabled.\n");
};

hook install_success => sub {
    my $args = shift;
    notify($args, "Successfully installed %s");
};

hook build_failure => sub {
    my $args = shift;
    notify($args, "Building %s failed.");
};

sub notify {
    my($args, $template, $is_sticky) = @_;

    return unless $notify;

    my $dist = "$args->{meta}->{name}-$args->{meta}->{version}";
    $dist = $args->{module} if $dist eq '-';
    my $msg = sprintf $template, $dist;

    $notify->create(
        timeout => -1,
        summary => $msg,
    )->show;
}
