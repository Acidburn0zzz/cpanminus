# NAME: fresh - Fetch the latest release from http://friendfeed.com/cpan
# AUTHOR: Tatsuhiko Miyagawa

my $entries;

hook search_module => 0, sub {
    my $args = shift;

    push @{$args->{uris}}, sub {
        my $dist = $args->{module};
        $dist =~ s/::/-/g;

        require JSON;

        unless ($entries) {
            $args->{app}->chat("Querying http://friendfeed.cpan/cpan for $dist...\n");
            my $json = Util::get("http://friendfeed-api.com/v2/feed/cpan");
            $entries = eval { JSON::decode_json($json)->{entries} } || [];
        }

        for my $entry (@$entries) {
            my $info = parse_entry($entry->{body}) or next;
            if ($info->{dist} eq $dist) {
                $args->{app}->chat("Found $dist on CPAN realtime feed => $info->{url}\n");
                return $info->{url};
            }
        }

        return;
    };
};

sub parse_entry {
    my $body = shift;

    if ($body =~ /^([\w\-]+) ([0-9\._]*) by (.+?) - <a.*href="(http:.*?\.tar\.gz)"/) {
        return {
            dist    => $1,
            version => $2,
            author  => $3,
            url     => $4,
        };
    }

    return;
}
