name        'dzil';
description 'Build dzil based distributions from git/svn repositories';
author      'Tatsuhiko Miyagawa';

api_version 0.1;

hook pre_configure => sub {
    my $args = shift;

    if (-e "dist.ini" && !-e 'Makefile.PL' && !-e 'Build.PL') {
        $args->{app}->chat("Found dist.ini - build this distribution using dzil");
        push @{$args->{deps}}, 'Dist::Zilla', 0;

        require Config::INI::Reader;
        my $hash = Config::INI::Reader->read_file("dist.ini");

        while (my($k, $v) = each %{$hash->{_}}) {
            $args->{meta}->{$k} = $v;
        }

        for my $key (keys %$hash) {
            next if $key eq '_';
            if ($key =~ s/^\@//) {
                push @{$args->{deps}}, "Dist::Zilla::PluginBundle::$key", 0;
            } else {
                push @{$args->{deps}}, "Dist::Zilla::Plugin::$key", 0;
            }
        }
    }
};

hook build_dist => sub {
    my $args = shift;

    if (-e "dist.ini" && !-e 'Makefile.PL' && !-e 'Build.PL') {
        $args->{app}->chat("Running dzil install");
        return sub {
            system "dzil", "install", "--install-command=$^X -S cpanm .";
        };
    }

    return;
};


