# NAME: minicpan - fallback to local minicpan when a dist is not found
# AUTHOR: Tatsuhiko Miyagawa

my $minicpan_home = $ENV{PERL_MINICPAN_HOME} || File::Spec->catfile($ENV{HOME}, "minicpan");

hook search_module => sub {
    my $args = shift;

    my $pkgs = File::Spec->catfile($minicpan_home, "modules", "02packages.details.txt.gz");
    return unless -e $pkgs;

    require IO::Zlib;

    push @{$args->{uris}}, sub {
        my $fh = IO::Zlib->new($pkgs, 'rb') or return;
        $args->{app}->chat("Scanning $pkgs to find $args->{module}...\n");
        while (<$fh>) {
            my($module, $ver, $dist) = split /\s+/, $_;
            if ($module eq $args->{module}) {
                $args->{app}->chat("Found $module on minicpan index: $dist\n");
                return "file://" . File::Spec->catfile($minicpan_home, "authors", "id", $dist);
            }
        }
        return;
    };

    return;
};
